apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    // Android App Bundle
    bundle {
        // Do not create a new APK for each language
        language {
            enableSplit = false;
        }
    }

    defaultConfig {
        applicationId "com.tycho.app.primenumberfinder"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 45
        versionName "1.5.2"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        externalNativeBuild{
            cmake {
                arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
            }
        }

        // Only build libraries for these architectures
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                //strippedNativeLibsDir 'build\\intermediates\\stripped_native_libs\\release\\out\\lib' //TODO: add symbols for native-utils
                //unstrippedNativeLibsDir 'build\\intermediates\\cmake\\release\\obj'
                strippedNativeLibsDir 'src/main/jniLibs'
                unstrippedNativeLibsDir 'src/main/jniLibs'
            }

            ndk {
                debugSymbolLevel 'FULL'
            }
        }

        debug {
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir 'src/main/jniLibs'
                unstrippedNativeLibsDir 'src/main/jniLibs'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    ndkVersion '21.0.6113669'

    packagingOptions {
        pickFirst '**/libTasker.so'
    }
}

// Upload Crashlytics NDK symbols
tasks.whenTaskAdded { task -> if (task.name.startsWith('assemble')) task.finalizedBy "uploadCrashlyticsSymbolFile" + task.name.substring('assemble'.length()) }

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:26.1.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Unit testing
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
